;Source: https://wiki.osdev.org/Interrupts
;
;   There are generally three classes of interrupts on most platforms:
;   ------------------------------------------------------------------
;
;   * Exceptions:
;     -----------
;
;     These are generated internally by the CPU and used to alert the running kernel of an event or situation which requires its attention
;     These include exception conditions such as Double Fault, Page Fault, General Protection Fault, etc.
;   
;   * Interrupt Request (IRQ) or Hardware Interrupt:
;     ----------------------------------------------
;
;   These are generated by Hardware and with the help of master PIC and slave PIC are handed to CPU
;
;   * Software Interrupt:
;     -------------------  
;
;   This is an interrupt signaled by software running on a CPU to indicate that it needs the kernel's attention.
;   These types of interrupts are generally used for System Calls.
;
;   Small note: Common practice is to leave the first *32 vectors for exceptions*, as mandated by Intel.
;               However you partition of the rest of the vectors is up to you.
;
;






%macro ISR_NO_ERRCODE 1
    global isr%1
    isr%1:
        cli
        push byte 0 ; error code is zero
        push byte %1 ; ISR number
        jmp isr_common_stub
%endmacro

%macro ISR_ERRCODE 1
    global isr%1
    isr%1:
        cli
        ;error code has already been pushed
        push byte %1 ; ISR number
        jmp isr_common_stub
%endmacro


ISR_NO_ERRCODE 0
ISR_NO_ERRCODE 1
ISR_NO_ERRCODE 2
ISR_NO_ERRCODE 3
ISR_NO_ERRCODE 4
ISR_NO_ERRCODE 5
ISR_NO_ERRCODE 6
ISR_NO_ERRCODE 7
ISR_ERRCODE 8
ISR_NO_ERRCODE 9
ISR_ERRCODE 10
ISR_ERRCODE 11
ISR_ERRCODE 12
ISR_ERRCODE 13
ISR_ERRCODE 14
ISR_NO_ERRCODE 15
ISR_NO_ERRCODE 16
ISR_NO_ERRCODE 17
ISR_NO_ERRCODE 18
ISR_NO_ERRCODE 19
ISR_NO_ERRCODE 20
ISR_NO_ERRCODE 21
ISR_NO_ERRCODE 22
ISR_NO_ERRCODE 23
ISR_NO_ERRCODE 24
ISR_NO_ERRCODE 25
ISR_NO_ERRCODE 26
ISR_NO_ERRCODE 27
ISR_NO_ERRCODE 28
ISR_NO_ERRCODE 29
ISR_NO_ERRCODE 30
ISR_NO_ERRCODE 31


extern isr_handler

isr_common_stub:
    ;save CPU state
    pusha ; pushes edi, esi, ebp, esp, ebx, edx, ecx, eax
    mov ax, ds ; lower 16-bits of eax = ds
    push eax ; save data segment descriptor

    mov ax, 0x10
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax

    ; call C function
    call isr_handler

    ; Restore state
    pop eax
    mov ds, ax
    mov es, ax
    mov fs, ax
    mov gs, ax
    popa

    add esp, 8 ; clear pushed error code and pushed ISR number
    sti
    iret ;pops 5 things at once: CS, EIP, EFLAGS, SS, ESP

